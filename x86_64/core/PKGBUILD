# Maintainer: Artoo <artoo@artixlinux.org>
# Contributor: Christian Hesse <mail@eworm.de>
# Contributor: Dave Reisner <dreisner@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>

_pkgbase=systemd-stable

pkgbase=udev
pkgname=('udev' 'libudev' 'esysusers' 'etmpfiles')
pkgdesc='Userspace device file manager'
_tag='20e3fdcb595febff92fe3e89ee33da2c04c5c682' # git rev-parse v${_tag_name}
_tag_name=250.5
pkgver="${_tag_name/-/}"
pkgrel=3
arch=('x86_64')
url='https://www.github.com/systemd/systemd'
license=('GPL2' 'LGPL2.1')
makedepends=('acl' 'libacl.so' 'kmod' 'libkmod.so' 'util-linux' 'libblkid.so'
             'hwdata' 'libcap' 'libcap.so' 'kbd' 'gperf' 'intltool' 'git'
             'meson' 'docbook-xsl' 'rsync' 'python-jinja')
options=('strip')
validpgpkeys=('63CDA1E5D3FC22B998D20DD6327F26951A015CC4'  # Lennart Poettering <lennart@poettering.net>
              'A9EA9081724FFAE0484C35A1A81CEA22BC8C7E2E'  # Luca Boccassi <luca.boccassi@gmail.com>
              '5C251B5FC54EB2F80F407AAAC54CA336CFEB557E') # Zbigniew JÄ™drzejewski-Szmek <zbyszek@in.waw.pl>
_commit=29cd296aa4635fe6f7b53bda2f2cb1648bdc0782
source=("git+https://github.com/systemd/systemd-stable#tag=${_tag}?signed"
        "git+https://github.com/systemd/systemd#tag=v${_tag_name%.*}?signed"
        '0001-Use-Arch-Linux-device-access-groups.patch'
        initcpio-{hook,install}-udev
        "git+https://gitea.artixlinux.org/artix/alpm-hooks.git#commit=$_commit")
sha512sums=('SKIP'
            'SKIP'
            '10f3b477527ec263cc6465c84d94416e356435930edc9e26844a0fd4f71e87a27fa0f91ce24b43a22cacdd2ead5e760e9d607369bc537a8da8d34021302a89a1'
            '32606b42856b5f3ea7f485143e532671f58986237e14c58ea5ab17383dc39a375cb6c738c8a2db9e4a8c8be88ea44a876d6bbed129cb2f5c9aa3f8228b04d927'
            '38eed28d42ac8f70bc8d1058ace35f137f7f5c972442ee14b98c2146202e0615aa584304edbd59e8608d1b6bec3cb391fc69b25393740f6eabd8fc5ad3bde64f'
            'SKIP')

_backports=(
)

_reverts=(
)

prepare() {
    cd "$_pkgbase"

    # add upstream repository for cherry-picking
    git remote add -f upstream ../systemd

    local _c
    for _c in "${_backports[@]}"; do
        git log --oneline -1 "${_c}"
        git cherry-pick -n "${_c}"
    done
    for _c in "${_reverts[@]}"; do
        git log --oneline -1 "${_c}"
        git revert -n "${_c}"
    done

    # Replace cdrom/dialout/tape groups with optical/uucp/storage
    patch -Np1 -i ../0001-Use-Arch-Linux-device-access-groups.patch
}

build() {
    local _meson_options=()

    _meson_options+=(
        -Dversion-tag="${_tag_name/-/\~}-${pkgrel}-artix"
        -Dmode=release

        -Dstandalone-binaries=true
        -Dsysusers=true
        -Dtmpfiles=true

        -Dadm-group=false
        -Dwheel-group=false
        -Dgshadow=false
        -Dhwdb=true
        -Dblkid=true
        -Dlink-udev-shared=false

        -Dman=false
        -Dtests=true

        -Ddns-servers=''
        -Dntp-servers=''
        -Defi=false

        -Dsysvinit-path=

        -Danalyze=false
        -Dapparmor=false
        -Daudit=false
        -Dbacklight=false
        -Dbinfmt=false
        -Dbzip2=false
        -Dcoredump=false
        -Ddbus=false
        -Delfutils=false
        -Denvironment-d=false
        -Dfdisk=false
        -Dgcrypt=false
        -Dglib=false
        -Dgnutls=false
        -Dhibernate=false
        -Dhostnamed=false
        -Didn=false
        -Dima=false
        -Dinitrd=false
        -Dfirstboot=false
        -Dkernel-install=false
        -Dldconfig=false
        -Dlibcryptsetup=false
        -Dlibcurl=false
        -Dlibfido2=false
        -Dlibidn=false
        -Dlibidn2=false
        -Dlibiptc=false
        -Dlocaled=false
        -Dlogind=false
        -Dlz4=false
        -Dmachined=false
        -Dmicrohttpd=false
        -Dnetworkd=false
        -Dnscd=false
        -Dnss-myhostname=false
        -Dnss-resolve=false
        -Dnss-systemd=false
        -Doomd=false
        -Dopenssl=false
        -Dp11kit=false
        -Dpam=false
        -Dpcre2=false
        -Dpolkit=false
        -Dportabled=false
        -Dpstore=false
        -Dpwquality=false
        -Drandomseed=false
        -Dresolve=false
        -Drfkill=false
        -Dseccomp=false
        -Dsmack=false
        -Dsysext=false
        -Dtimedated=false
        -Dtimesyncd=false
        -Dtpm=false
        -Dqrencode=false
        -Dquotacheck=false
        -Duserdb=false
        -Dutmp=false
        -Dvconsole=false
        -Dxdg-autostart=false
        -Dxkbcommon=false
        -Dxz=false
        -Dzlib=false
        -Dzstd=false
    )

    artix-meson "$_pkgbase" build "${_meson_options[@]}"

    local _targets=()

    _targets+=(
        udev:shared_library
        src/libudev/libudev.pc
        udevadm
        systemd-hwdb
        src/udev/{ata_id,cdrom_id,fido_id,mtd_probe,dmi_memory_id,scsi_id,v4l_id}
        src/udev/udev.pc
        rules.d/{50-udev-default.rules,64-btrfs.rules}
        hwdb.d/60-autosuspend-chromiumos.hwdb
        man/{libudev.3,udev.conf.5,hwdb.7,udev.7,udevadm.8}

        systemd-{sysusers,tmpfiles}.standalone
        sysusers.d/basic.conf
        tmpfiles.d/{etc,static-nodes-permissions,var}.conf
        man/{sysusers,tmpfiles}.d.5
        man/systemd-{sysusers,tmpfiles}.8

        systemd-detect-virt
        test/sys
        test-udev
        test-fido-id-desc
        test-udev-builtin
        test-udev-event
        test-udev-netlink
        test-udev-node
        test-udev-util
        systemd-runtest.env
        test-tmpfiles
    )

    meson compile -C build "${_targets[@]}"
}

check() {
    local tests=()
    tests+=(
        test-sysusers.standalone
        test-systemd-tmpfiles.standalone
        test-tmpfiles
        rule-syntax-check
        test-fido-id-desc
        test-udev-builtin
        test-udev-event
        test-udev-netlink
        test-udev-node
        test-udev-util
        udev-test
        test-libudev
        test-libudev-sym
        test-udev-device-thread
    )
    meson test -C build --print-errorlogs "${tests[@]}"
}

package_udev() {
    pkgdesc='Userspace device file manager'
    depends=('acl' 'libacl.so' 'kmod' 'libkmod.so'
            'util-linux' 'libblkid.so' 'libudev' 'hwdata' 'kbd')
    provides=("udev=$pkgver")

    install -vd "${pkgdir}"/etc/udev/{hwdb,rules}.d
    install -vm644 "$_pkgbase"/src/udev/udev.conf "${pkgdir}"/etc/udev

    install -vd "${pkgdir}"/usr/bin
    install -vm755 build/udevadm "${pkgdir}"/usr/bin/udevadm
    install -vm755  build/systemd-hwdb  "${pkgdir}"/usr/bin/udev-hwdb
    ln -sfv udevadm "${pkgdir}"/usr/bin/udevd

    install -vd "${pkgdir}"/usr/{share,lib}/pkgconfig
    install -vm644 build/src/libudev/libudev.pc "${pkgdir}"/usr/lib/pkgconfig
    install -vm644 build/src/udev/udev.pc "${pkgdir}"/usr/share/pkgconfig

    install -vd "${pkgdir}"/usr/lib/udev
    install -vm755 build/src/udev/{*_id,mtd_probe} "${pkgdir}"/usr/lib/udev

    install -vd "${pkgdir}"/usr/lib/udev/{hwdb,rules}.d
    install -vm644 build/hwdb.d/* "${pkgdir}"/usr/lib/udev/hwdb.d
    install -vm644 "$_pkgbase"/hwdb.d/{*.hwdb,README} "${pkgdir}"/usr/lib/udev/hwdb.d
    install -vm644 build/rules.d/* "${pkgdir}"/usr/lib/udev/rules.d
    install -vm644 "$_pkgbase"/rules.d/{*.rules,README} "${pkgdir}"/usr/lib/udev/rules.d
    rm -vf "${pkgdir}"/usr/lib/udev/rules.d/99-systemd.rules

    install -vd "${pkgdir}"/usr/include
    install -vm644 "$_pkgbase"/src/libudev/libudev.h "${pkgdir}"/usr/include

    install -vd "${pkgdir}"/usr/share/{doc/"${pkgname}",man/man{3,5,7,8}}
    install -vm644 build/man/libudev.3 "${pkgdir}"/usr/share/man/man3
    install -vm644 build/man/udev.conf.5 "${pkgdir}"/usr/share/man/man5
    install -vm644 build/man/udev.7 "${pkgdir}"/usr/share/man/man7
    install -vm644 build/man/udevadm.8 "${pkgdir}"/usr/share/man/man8

    install -vm644 "$_pkgbase"/LICENSE.* "${pkgdir}"/usr/share/doc/"${pkgname}"

    # initcpio
    install -vD -m0644 initcpio-install-udev "${pkgdir}"/usr/lib/initcpio/install/udev
    install -vD -m0644 initcpio-hook-udev "${pkgdir}"/usr/lib/initcpio/hooks/udev

    # pacman hooks
    make -C alpm-hooks DESTDIR="${pkgdir}" install_udev
}

package_libudev() {
    pkgdesc='udev library for enumerating and introspecting local devices'
    depends=('gcc-libs')
    provides=('libudev.so')

    install -vd "${pkgdir}"/usr/lib
    install -vm644 build/$(readlink build/libudev.so.1) "${pkgdir}"/usr/lib
    ln -sfv $(readlink build/libudev.so.1) "${pkgdir}"/usr/lib/libudev.so.1
    ln -sfv libudev.so.1 "${pkgdir}"/usr/lib/libudev.so
}

package_esysusers() {
    pkgdesc='the sysusers.d binary'
    groups=('base-devel')
    depends=('gcc-libs' 'libxcrypt')

    install -vd "${pkgdir}"/usr/bin
    install -vm755 build/systemd-sysusers.standalone "${pkgdir}"/usr/bin/sysusers

    install -vd "${pkgdir}"/usr/lib/sysusers.d
    install -vm644 build/sysusers.d/basic.conf "${pkgdir}"/usr/lib/sysusers.d
    install -vm644 "$_pkgbase"/sysusers.d/README "${pkgdir}"/usr/lib/sysusers.d

    install -vd "${pkgdir}"/usr/share/{doc/"${pkgname}",man/man{5,8}}
    install -vm644 build/man/sysusers.d.5 "${pkgdir}"/usr/share/man/man5
    install -vm644 build/man/systemd-sysusers.8 "${pkgdir}"/usr/share/man/man8/sysusers.8
    install -vm644 "$_pkgbase"/LICENSE.* "${pkgdir}"/usr/share/doc/"${pkgname}"

    # pacman hooks
    make -C alpm-hooks DESTDIR="${pkgdir}" install_sysusers
}

package_etmpfiles() {
    pkgdesc='the tmpfiles.d binary'
    groups=('base-devel')
    depends=('acl' 'libacl.so' 'libcap' 'libcap.so')

    install -vd "${pkgdir}"/usr/bin
    install -vm755 build/systemd-tmpfiles.standalone "${pkgdir}"/usr/bin/tmpfiles

    install -vd "${pkgdir}"/{etc,usr/lib}/tmpfiles.d
    install -vm644 build/tmpfiles.d/* "${pkgdir}"/usr/lib/tmpfiles.d
    install -vm644 "$_pkgbase"/tmpfiles.d/{{tmp,home,x11}.conf,README} "${pkgdir}"/usr/lib/tmpfiles.d

    install -vd "${pkgdir}"/usr/share/{doc/"${pkgname}",man/man{5,8}}
    install -vm644 build/man/tmpfiles.d.5 "${pkgdir}"/usr/share/man/man5
    install -vm644 build/man/systemd-tmpfiles.8 "${pkgdir}"/usr/share/man/man8/tmpfiles.8
    install -vm644 "$_pkgbase"/LICENSE.* "${pkgdir}"/usr/share/doc/"${pkgname}"

    # pacman hooks
    make -C alpm-hooks DESTDIR="${pkgdir}" install_tmpfiles
}
